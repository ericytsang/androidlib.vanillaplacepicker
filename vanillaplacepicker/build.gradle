apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.Mindinventory'
version='0.0.8-SNAPSHOT'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 7
        versionName "0.0.8-SNAPSHOT"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.31'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha05'
    implementation 'androidx.core:core-ktx:1.2.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    testImplementation 'junit:junit:4.12'

    // Location
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'

    // this library are used for Retrofit API calling feature
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'

    // Google material
    implementation 'com.google.android.material:material:1.0.0'
}
repositories {
    mavenCentral()
}

// Gradle task to check Google API key availability in AndroidManifest of root project
task validateManifest() {

    def xmlSource = file("${rootDir}/app/src/main/AndroidManifest.xml")
    def xmlParser = new XmlParser()
    def xmlRoot = xmlParser.parse(xmlSource)
    def metaDataNode = xmlRoot.application.'meta-data'
    def isAPIKeyAvailable = false
    metaDataNode.any { metadata ->

        metadata.attributes().any {

            if (it.value == "com.google.android.geo.API_KEY") {
                isAPIKeyAvailable = true
                return true
            } else isAPIKeyAvailable = false
        }

        if(isAPIKeyAvailable)
            return true
    }

    if (!isAPIKeyAvailable)
        throw new RuntimeException("Couldn\'t get Google API key from application meta data. Was it set in your AndroidManifest.xml?")
}
